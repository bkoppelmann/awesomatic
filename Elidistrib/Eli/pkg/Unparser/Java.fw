@A@<Create a Java Structural Unparser@>

The FunnelWeb file defining a Java structural unparser begins with a brief
description of how to output unparsed values, and specifies
the types of the attributes holding those values.
It then defines
the tree to be unparsed,
the class symbol computations,
and which tree symbols inherit which class symbol computations.
PTG templates follow these definitions.
Finally, the file defines the @{IGNORE@} macro and instantiates the
necessary library modules.

@$@<Complete FunnelWeb file@>==@{
@=~
Frame:
  "@p maximum_input_line_length = infinity\n\n"
  "The following symbol computation can be used to display the entire input:"
  "\n\nSYMBOL ROOTCLASS COMPUTE\n"
  "  BP_Out(THIS." $1 string "Ptg);\n"
  "END;\n\n"
  "Applying this output function to the " $1 string
    "Ptg attribute of any nonterminal\n"
  "symbol in the tree will cause the subtree rooted in that node to be"
    " displayed.\n\n"
  "@O@<" $1 string "copy.lido@>@{\n"
  "ATTR " $1 string "Ptg, " $1 string "OrigPtg: PTGNode;\n\n"
  $2 "\n"	/* Tree grammar */
  $3		/* Class computations */
  $4		/* Inheritances */
  "@}\n\n"
  "@O@<" $1 string "copy.ptg@>@{\n"
  $5		/* PTG templates */
  "@}\n\n"
  "@O@<" $1 string "copy.ptg.phi@>@{\n"
  "#define IGNORE(f,n)\n"
  "@}\n\n"
  "@O@<" $1 string "copy.specs@>@{\n"
  "$/Output/PtgCommon.fw\n"
  "$/Output/BlockPrint.fw\n"
  "@}\n"
~=@
@}

The FunnelWeb file defining the generated unparser is written as file
@{tree@} in the current working directory: 

@$@<Output the complete FunnelWeb file@>==@{
SYMBOL xRoot COMPUTE
  PTGOutFile(
    "tree",
    PTGFrame(
      THIS.Prefix,
      THIS.TreeGram,
      TAIL.ClassComp,
      TAIL.Inherit,
      TAIL.PtgTemplates));
END;
@}

@B@<PTG templates for non-literal terminal symbols@>

The same PTG template is generated for each non-literal terminal symbol:

@$@<Format for a non-literal terminal symbol@>==@{
Term: $ string "_" $ ":\n \"\\\"\" [PtgOutId $ int] \"\\\"\"\n"
@}

This template assumes that the internal representation of a
non-literal terminal symbol is a string table index.
The user must override it for any non-literal terminal symbol with a
different internal representation.

@$@<Create PTG templates@>+=@{
SYMBOL xSymbol COMPUTE
  THIS.PtgTemplates =
    IF(AND(THIS.IsTerminal,AND(THIS.IsFirstOcc,GetNoPtgGiven(THIS.Key,1))),
      PTGSeq(THIS.PtgTemplates,PTGTerm(INCLUDING xRoot.Prefix,THIS.Ptg)),
      THIS.PtgTemplates)
    <- INCLUDING xRoot.GotProperties;
END;
@}

@B@<PTG templates for plain productions@>

The body of the template for a plain production is a Java @{new@} operation
that dynamically allocates an object of the appropriate production class.
The arguments of the constructor function are defined by the symbols on the
right-hand side of the plain production.
In order to increase the readability of the output, the line may be broken
after each argument:

@$@<Formats for productions@>+=@{
PlainProd:
  $1 string "_" $2 string ":\n"
  "  \"new " $2 string "(\" [BP_BeginBlockI] [BP_BreakLine] "
    $3 " [BP_BreakLine] [BP_EndBlockI] \")\"\n"

SepSeq:
  $ { " \",\" [BP_BreakLine] " } $
@}

Each argument of the constructor function is the value of the @{Insert@}
attribute of the corresponding element on the right-hand side
of the plain production:

@$@<Create PTG templates@>+=@{
ATTR Insert: PTGNode;

RULE: xProduction ::=  'RULE' xRule ':' xSymbol '::=' xRHS 'END' ';'
COMPUTE
  xProduction.PtgTemplates=
    IF(GetNoPtgGiven(xRule.Key,1),
      PTGSeq(
        xRHS.PtgTemplates,
        PTGPlainProd(
          INCLUDING xRoot.Prefix,
          StringTable(xRule.Sym),
          xRHS CONSTITUENTS Element.Insert
            WITH (PTGNode, PTGSepSeq, IDENTICAL, PTGNull))),
      xRHS.PtgTemplates)
    <- INCLUDING xRoot.GotProperties;
END;
@}

All literals on the right-hand side are ignored, but the representation of
any other symbol is simply copied into the constructor's argument list:

@$@<Create PTG templates@>+=@{
RULE: Element ::= xLiteral
COMPUTE
  Element.Insert = PTGNULL;
END;

RULE: Element ::= xSymbol
COMPUTE
  Element.Insert = Element.Mark;
END;

RULE: Element ::= '$' xSymbol
COMPUTE
  Element.Insert = Element.Mark;
END;
@}

@B@<PTG templates for LISTOF productions@>

There are three templates for each @{LISTOF@} production, and none of their
bodies depend on the right-hand side of that production:

@$@<Formats for productions@>+=@{
ListProd:
  $1 string "_"  $2 string ":\n  \"new " $2 string
    "(\" [BP_BeginBlockI] [BP_BreakLine] "
    "$ [BP_BreakLine] [BP_EndBlockI] \")\"\n"

ListProd2:
  $1 string "_2" $2 string ":\n  { \"" $1 string ".Comb" $1 string
    "(\" } $ { \",\" [BP_BreakLine] } $ { \")\" }\n" 

ListProd1:
  $1 string "_1" $2 string ":\n  \"" $1 string ".Single" $1 string
    "(\" [BP_BeginBlockI] [BP_BreakLine] "
    "$ [BP_BreakLine] [BP_EndBlockI] \")\"\n"

Triple: $ $ $
@}

Each one of the PTG templates can be overridden individually:

@$@<Create PTG templates@>+=@{
RULE: xProduction ::=  'RULE' xRule ':' xSymbol 'LISTOF' Syms 'END' ';'
COMPUTE
  xProduction.PtgTemplates=
    PTGSeq(
      Syms.PtgTemplates,
      PTGTriple(
        IF(GetNoPtgGiven(xRule.Key,1),
          PTGListProd(INCLUDING xRoot.Prefix,StringTable(xRule.Sym)),
          PTGNULL),
        IF(GetNoPtg2Given(xRule.Key,1),
          PTGListProd2(INCLUDING xRoot.Prefix,StringTable(xRule.Sym)),
          PTGNULL),
        IF(GetNoPtg1Given(xRule.Key,1),
          PTGListProd1(INCLUDING xRoot.Prefix,StringTable(xRule.Sym)),
          PTGNULL)))
    <- INCLUDING xRoot.GotProperties;
END;
@}

@A@<Create a Java Tree Module@>

The Java tree module consists of a base class, a visitor class, classes for 
grammar symbols and classes for productions.
The Java files are written to the @{treeDef@} sub-directory of the current
directory:

@$@<Compute the common start of the module file names@>==@{
SYMBOL xRoot:
  HasListof:      int,		/* 1 if there are LISTOF productions */
  ModuleBasename: CharPtr;	/* Basename of the module files */

SYMBOL xRoot COMPUTE
  SYNT.HasListof=
    CONSTITUENTS xProduction.IsListof WITH (int, OR, IDENTICAL, ZERO);
  SYNT.ModuleBasename="treeDef/";
END;

SYMBOL xProduction:
  IsListof:       int;		/* 1 if this is a LISTOF production */

RULE: xProduction ::=  'RULE' xRule ':' xSymbol '::=' xRHS 'END' ';'
COMPUTE
  xProduction.IsListof=0;
END;

RULE: xProduction ::=  'RULE' xRule ':' xSymbol 'LISTOF' Syms 'END' ';'
COMPUTE
  xProduction.IsListof=1;
END;
@}

@B@<Module interface@>

The tree structure definition consists of
a base class declaration,
a set of abstract symbol class declarations,
and a set of production class declarations.

If the tree grammar contains any @{LISTOF@} productions, then a combine
function declaration immediately follows the base class declaration.

@$@<Structure of the document type definition@>==@{
Header:
  "package " $1 string ";\n\n"
  "import java.util.LinkedList;\n\n"
  "abstract public class " $1 string " {\n"
  "  abstract public void accept(" $1 string "Visitor a);\n"
  $2	/* Possible combine function declaration */
  "}\n"

Forward:
  "class " $1 string ";\n"

DeclareCombine:
  "  public static LinkedList Single" $1 string "(" $1 string " a)\n"
  "    { LinkedList t = new LinkedList(); t.add(a); return t; }\n"
  "  public static LinkedList Comb" $1 string "(LinkedList a, LinkedList b)\n"
  "    { a.addAll(b); return a; }\n"

Visitor:
  "package " $1 string ";\n\n"
  "abstract public class " $1 string "Visitor {\n"
  $2	/* Production class visitor methods */
  "}\n"

Visit:
  "    abstract public void Visit" $1 string "(" $1 string " a);\n"
@}


@$@<Output the complete class file@>==@{
ATTR Fwd, Visit: PTGNode;

SYMBOL xRoot COMPUTE
  PTGOutFile(
    CatStrStr(
      THIS.ModuleBasename,
      CatStrStr(THIS.Prefix,".java")),
    PTGHeader(
      THIS.Prefix,
      IF(THIS.HasListof,PTGDeclareCombine(THIS.Prefix),PTGNULL)));
  PTGOutFile(
    CatStrStr(
      THIS.ModuleBasename,
      CatStrStr(THIS.Prefix,"Visitor.java")),
    PTGVisitor(
      THIS.Prefix,
      CONSTITUENTS xRule.Visit WITH(PTGNode,PTGSeq,IDENTICAL,PTGNull)));
END;

SYMBOL xRule COMPUTE
  SYNT.Fwd=PTGForward(StringTable(THIS.Sym));
  SYNT.Visit=PTGVisit(StringTable(THIS.Sym));
END;
@}

@B@<Symbol classes@>

Each non-terminal symbol class inherits from the base class.
It has no variables or methods of its own.

@$@<Format of a symbol class definition@>==@{
SymbolClass:
  "package " $2 string ";\n\n"
  "public abstract class " $1 " extends " $2 string " { }\n"
@}

@$@<Construct the symbol class definitions@>==@{
SYMBOL xSymbol COMPUTE
  IF(AND(THIS.IsFirstOcc,NOT(THIS.IsTerminal)),
    PTGOutFile(
      CatStrStr(
        INCLUDING xRoot.ModuleBasename,
        CatStrStr(StringTable(THIS.Sym), ".java")),
      PTGSymbolClass(THIS.Ptg,INCLUDING xRoot.Prefix)));
END;
@}

@B@<Production classes@>

Each production class inherits from the class corresponding to the symbol
on the left-hand side.
It may have children, in which case the constructor will have arguments and
assign the values of those arguments to the corresponding children.
Each child will be declared as a private variable and an public access
method for that child will be declared.

Every production class implements an @{Access@} method invoking the visitor
method corresponding to the production.

@$@<Format of a production class definition@>+=@{
ProductionClass:
  "package " $7 string ";\n\n"
  "public final class " $1 string " extends " $2 " {\n"
  "  public " $1 string "(" $3 ")\n"
  "    { " $4 "}\n"
  "  public void accept(" $7 string "Visitor v)"
         " { v.Visit" $1 string "(this); }\n"
  $5	/* Access functions */
  $6	/* Fields */
  "}\n"

Argument:
  $1 " a" $2 int

Assign:
  "c" $1 int " = a" $1 int "; "

Computed:
  "c" $2 int " = (" $1 ")0; "

Access:
  "  public " $1 " getChild" $2 int "() { return c" $2 int "; }\n"

Set:
  "  public void setChild" $2 int "(" $1 " a) { c" $2 int " = a; }\n"

Declare:
  "  private " $1 " c" $2 int ";\n"
@}

@$@<Construct the element definitions@>+=@{
CHAIN cnt: int;
ATTR Argument, Assign, Access, Declare: PTGNode;

RULE: xProduction ::=  'RULE' xRule ':' xSymbol '::=' xRHS 'END' ';'
COMPUTE
  .Declare=
    xRHS CONSTITUENTS Element.Declare
      WITH(PTGNode, PTGSeq, IDENTICAL, PTGNull);
  CHAINSTART xRHS.cnt=1;
  PTGOutFile(
    CatStrStr(
      INCLUDING xRoot.ModuleBasename,
      CatStrStr(StringTable(xRule.Sym), ".java")),
    PTGProductionClass(
      StringTable(xRule.Sym),
      xSymbol.Ptg,
      xRHS CONSTITUENTS Element.Argument
        WITH(PTGNode, PTGCommaSeq, IDENTICAL, PTGNull),
      xRHS CONSTITUENTS Element.Assign
        WITH(PTGNode, PTGSeq, IDENTICAL, PTGNull),
      xRHS CONSTITUENTS Element.Access
        WITH(PTGNode, PTGSeq, IDENTICAL, PTGNull),
      IF(EQ(.Declare,PTGNULL),PTGNULL,.Declare),
      INCLUDING xRoot.Prefix));
END;

SYMBOL Element COMPUTE
  THIS.cnt=ADD(THIS.cnt,1);
END;

RULE: Element ::= xLiteral
COMPUTE
  Element.Argument = PTGNULL;
  Element.Assign = PTGNULL;
  Element.Access = PTGNULL;
  Element.Declare = PTGNULL;
END;

SYMBOL xSymbol: Type: PTGNode;

SYMBOL xSymbol COMPUTE
  SYNT.Type=IF(THIS.IsTerminal,PTGAsIs("String"),THIS.Ptg);
END;

RULE: Element ::= xSymbol
COMPUTE
  Element.Argument = PTGArgument(xSymbol.Type,Element.cnt);
  Element.Assign = PTGAssign(Element.cnt);
  Element.Access = PTGAccess(xSymbol.Type,Element.cnt);
  Element.Declare = PTGDeclare(xSymbol.Type,Element.cnt);
END;

RULE: Element ::= '$' xSymbol
COMPUTE
  Element.Argument = PTGNULL;
  Element.Assign = PTGComputed(xSymbol.Type,Element.cnt);
  Element.Access=
    PTGSeq(
      PTGSet(xSymbol.Type,Element.cnt),
      PTGAccess(xSymbol.Type,Element.cnt));
  Element.Declare = PTGDeclare(xSymbol.Type,Element.cnt);
END;
@}

@$@<Format of a production class definition@>+=@{
ListofProdClass:
  "package " $3 string ";\n\n"
  "import java.util.LinkedList;\n\n"
  "final public class " $1 string " extends " $2 " {\n"
  "  public " $1 string "(LinkedList a) { c = a; }\n"
  "  public void accept(" $3 string "Visitor v)"
         " { v.visit" $1 string "(this); }\n"
  "  public LinkedList getList() { return c; }\n"
  "  private LinkedList c;\n"
  "}\n"
@}

@$@<Construct the element definitions@>+=@{
RULE: xProduction ::=  'RULE' xRule ':' xSymbol 'LISTOF' Syms 'END' ';'
COMPUTE
  PTGOutFile(
    CatStrStr(
      INCLUDING xRoot.ModuleBasename,
      CatStrStr(StringTable(xRule.Sym), ".java")),
    PTGListofProdClass(
      StringTable(xRule.Sym),
      xSymbol.Ptg,
      INCLUDING xRoot.Prefix));
END;
@}

@B@<Module code@>

When a tree grammar contains @{LISTOF@} productions, the unparser must
build the list children of those productions by combining individual
objects.
Any list can be built up by converting each element to a single-element
list, and then combining pairs of lists.
Each list is only used as an operand of a @/single@/ combine operation.

The C++ Standard Template Library provides a @{splice@} function that
copies one list into another at a given position.
This function can be used to combine pairs of lists:

@$@<Function for combining lists@>==@{
Combine:
  "#include \"" $1 string ".h\"\n\n"
  "list<" $1 string " *> *\n"
  "Comb" $1 string "(list<" $1 string " *> *x, list<" $1 string " *> *y)\n"
  "{ x->splice(x->end(), *y, y->begin(), y->end()); y->~list(); return x; }\n"
@}

Here @{splice@} is used to copy the second list of the pair to the end of
the first list of the pair.
The modified first list then becomes the result of the combine operation.

Because each list is only used as an operand to the combine function once,
the second operand will never be used again and can therefore be destroyed.

The module code is contained in a type-@{cc@} file written to the
module sub-directory of the current working directory.
It is produced if and only if there are @{LISTOF@}
productions:

@$@<Output the complete program file@>==@{
ATTR Fwd, Visit: PTGNode;

/*
SYMBOL xRoot COMPUTE
  IF(THIS.HasListof,
    PTGOutFile(
      CatStrStr(THIS.ModuleBasename,".java"),
      PTGCombine(THIS.Prefix)));
END;
*/
@}

@A@<Files Defining a Java and Module@>

@O@<Cpp.ptg@>@{
@<Complete FunnelWeb file@>
@<Format for a non-literal terminal symbol@>
@<Formats for productions@>
@<Structure of the document type definition@>
@<Format of a symbol class definition@>
@<Format of a production class definition@>
@<Function for combining lists@>

AltSeq: $ { " | " } $
@}

@O@<Cpp.lido@>@{
@<Output the complete FunnelWeb file@>
@<Create PTG templates@>
@<Output the complete class file@>
@<Compute the common start of the module file names@>
@<Construct the symbol class definitions@>
@<Construct the element definitions@>
@<Output the complete program file@>
@}
